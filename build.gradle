import net.ltgt.gradle.errorprone.CheckSeverity

buildscript {
    repositories {
        gradlePluginPortal()
    }

    dependencies {
        classpath 'com.palantir.gradle.consistentversions:gradle-consistent-versions:2.11.0'
        classpath 'com.palantir.baseline:gradle-baseline-java:3.80.0'
        classpath 'com.palantir.javaformat:gradle-palantir-java-format:2.0.1'
        classpath 'nu.studer:gradle-jooq-plugin:3.0.3'
        classpath 'org.liquibase:liquibase-gradle-plugin:2.0.3'
        classpath 'org.unbroken-dome.gradle-plugins:gradle-testsets-plugin:4.0.0'
        classpath 'com.palantir.gradle.docker:gradle-docker:0.32.0'
        classpath 'com.palantir.sls-packaging:gradle-sls-packaging:7.4.0'
        classpath 'com.palantir.gradle.gitversion:gradle-git-version:0.12.3'
    }
}

plugins {
    id 'org.inferred.processors' version '3.3.0'
    id 'com.avast.gradle.docker-compose' version '0.14.3'
    id 'maven-publish'
}

apply plugin: 'com.palantir.baseline'
apply plugin: 'com.palantir.consistent-versions'
apply plugin: 'com.palantir.git-version'

group = 'com.fern.nursery'
version = gitVersion()

allprojects {
    repositories {
        mavenCentral()
        maven {
            url "https://maven.buildwithfern.com"
        }
        maven {
            url "https://maven-dev.buildwithfern.com"
        }
    }

    apply plugin: 'com.palantir.java-format'
    apply plugin: 'maven-publish'
    apply plugin: 'org.inferred.processors'

    version rootProject.version
    group rootProject.group

    pluginManager.withPlugin('java') {
        tasks.withType(JavaCompile) {
            options.compilerArgs += ['-Werror', '-Xlint:deprecation']
            options.errorprone {
                check("Slf4jLogsafeArgs", CheckSeverity.OFF)
                check("PreferSafeLoggableExceptions", CheckSeverity.OFF)
                check("StrictUnusedVariable", CheckSeverity.OFF)
                check("Slf4jConstantLogMessage", CheckSeverity.OFF)
            }
        }
    }
}
